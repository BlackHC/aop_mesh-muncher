splatterDefines:
	module

	code::
		#define CONSERVATIVE

splatterUtility:
	module

	code::
		int determineMainAxis( const vec3 faceNormal ) {
			const vec3 axisDot = abs( faceNormal );

			int mainAxis = 0;
			if( axisDot.x > axisDot.z ) {
				if( axisDot.y > axisDot.x ) {
					mainAxis = 1;
				}
			}
			else { // z > x
				if( axisDot.y > axisDot.z ) {
					mainAxis = 1;
				}
				else {
					mainAxis = 2;
				}
			}

			return mainAxis;
		}

		// outer normal for a CCW edge orientation with y up
		vec2 getOutwardNormal( const vec2 edge, const vec3 faceNormal ) {
			return normalize( cross( vec3( edge, 0.0 ), vec3( 0.0, 0.0, sign( faceNormal.z ) ) ).xy );
		}

		// return lamda * direction, such that lambda * direction perp projected onto v is v
		vec2 unproject( const vec2 direction, const vec2 v ) {
			if( dot( direction, v ) == 0 ) {
				return vec2( 0 );
			}
			return direction * dot( v, v ) / dot( direction, v );
		}

		vec3 getCornerShift(
			const vec3 faceNormal,
			const vec2 prevEdge,
			const vec2 nextEdge,
			const vec2 shiftPrevEdge,
			const vec2 shiftNextEdge
		) {
			vec3 cornerShift;
			cornerShift.xy = unproject( prevEdge, shiftNextEdge ) + unproject( nextEdge, shiftPrevEdge );

			// solve for z
			// we have: faceNormal . corner = faceNormal . (corner + cornerShift) <=> cornerShift.z = - faceNormal.xy . cornerShift.xy / faceNormal.z
			cornerShift.z = -dot( faceNormal.xy, cornerShift.xy ) / faceNormal.z;

			return cornerShift;
		}

splatterVertexShader:
	vertexShader

	code::
		void vertexShader() {
			gl_FrontColor = vec4( mat3(gl_ModelViewMatrix) * gl_Normal * 0.5 + 0.5, 1.0 ); // 0..1
			gl_Position = gl_ModelViewMatrix * gl_Vertex;
		}

splatterGeometryShader:
	geometryShader

	dependencies:
		splatterDefines
		splatterUtility

	code::
			uniform mat4 mainAxisProjection[3];
			uniform mat3 mainAxisPermutation[3];

			// these lines enable the geometry shader support.
			layout( triangles ) in;
			layout( triangle_strip, max_vertices=3 ) out;

			out vec3 viewPosition;
			// we can get rid of mainAxis, because gl_ViewportIndex is available in the fragment shader as well
			flat out int mainAxis;

		#ifdef CONSERVATIVE
			out BoundingBox {
				vec2 min, max;
			} boundingBox;
		#endif

			void geometryShader( void )
			{
				// the incoming position is the one using output grid coordinates
				const vec3 edgeA = vec3( gl_in[1].gl_Position - gl_in[0].gl_Position );
				const vec3 edgeB = vec3( gl_in[2].gl_Position - gl_in[0].gl_Position );

				// calculate the face normal
				const vec3 unpermutedFaceNormal = cross( edgeA, edgeB );

				// set the main axis
				mainAxis = determineMainAxis( unpermutedFaceNormal );

				// we use different viewports depending on the main axis, because the grid volume doesn't have to be a unit cube
				gl_ViewportIndex = mainAxis;

				// grab the projection and permutation matrix for this axis
				const mat4 projectionMatrix = mainAxisProjection[ mainAxis ];
				const mat3 permutationMatrix = mainAxisPermutation[ mainAxis ];

				// permute the corners
				vec3 corners[3];
				for( int i = 0 ; i < 3 ; i++ ) {
					corners[i] = permutationMatrix * gl_in[i].gl_Position.xyz;
				}

		#ifdef CONSERVATIVE
				// permute the face normal
				const vec3 faceNormal = permutationMatrix * unpermutedFaceNormal;

				const vec2 halfPixelSize = vec2( 0.5 );

				// set the bounding box
				boundingBox.min = min( corners[0].xy, min( corners[1].xy, corners[2].xy ) ) - halfPixelSize;
				boundingBox.max = max( corners[0].xy, max( corners[1].xy, corners[2].xy ) ) + halfPixelSize;

				// calculate the screen edges
				const vec2 screenEdges[3] = {
					corners[1].xy - corners[0].xy,
					corners[2].xy - corners[1].xy,
					corners[0].xy - corners[2].xy
				};

				// determine the outer normals
				const vec2 screenOutwardNormals[3] = {
					getOutwardNormal( screenEdges[0], faceNormal ),
					getOutwardNormal( screenEdges[1], faceNormal ),
					getOutwardNormal( screenEdges[2], faceNormal )
				};

				// determine the shift by which we move the edges
				const vec2 edgeShift[3] = {
					screenOutwardNormals[0] * dot( abs( screenOutwardNormals[0] ), halfPixelSize ),
					screenOutwardNormals[1] * dot( abs( screenOutwardNormals[1] ), halfPixelSize ),
					screenOutwardNormals[2] * dot( abs( screenOutwardNormals[2] ), halfPixelSize )
				};

				// expand the corners
				corners[0] += getCornerShift( faceNormal, screenEdges[2], screenEdges[0], edgeShift[2], edgeShift[0] );
				corners[1] += getCornerShift( faceNormal, screenEdges[0], screenEdges[1], edgeShift[0], edgeShift[1] );
				corners[2] += getCornerShift( faceNormal, screenEdges[1], screenEdges[2], edgeShift[1], edgeShift[2] );
		#endif

				for( int i = 0 ; i < gl_in.length() ; i++ )
				{
					gl_FrontColor = gl_in[i].gl_FrontColor;

					// store the expanded corners in the original world coords (multiply with the transposed permutation matrix)
					viewPosition = corners[i];

					// transform the view position into the unit cube
					gl_Position = projectionMatrix * vec4( corners[i], 1.0 );

					EmitVertex();
				}
			}

splatterFragmentShader:
	fragmentShader

	dependencies:
		splatterDefines
		splatterUtility

	code::
			uniform mat3 mainAxisPermutation[3];

			uniform volatile layout(r32ui) uimage3D volumeChannels[4]; // rgb hit

			flat in int mainAxis;
			in vec3 viewPosition;

		#ifdef CONSERVATIVE
			in BoundingBox {
				vec2 min, max;
			} boundingBox;
		#endif

			struct VoxelInfo {
				uvec3 normal;
			};

			VoxelInfo processFragment() {
				VoxelInfo result;
				// we have 32 bit, if we assume an overdraw of less than 256, we can use a resolution that is a lot higher than 2*256.0!
				result.normal = uvec3( clamp( normalize( gl_Color.xyz * 2.0f - 1.0f ) * 256.0f + 256.0f, 0.f, 512.f ) ); // 0..512
				return result;
			}

			void splatVoxel( const ivec3 voxelPosition, const VoxelInfo voxelInfo ) {
				imageAtomicAdd( volumeChannels[0], voxelPosition, voxelInfo.normal.r );
				imageAtomicAdd( volumeChannels[1], voxelPosition, voxelInfo.normal.g );
				imageAtomicAdd( volumeChannels[2], voxelPosition, voxelInfo.normal.b );
				imageAtomicAdd( volumeChannels[3], voxelPosition, 1u );
			}

			void fragmentShader() {
		#ifdef CONSERVATIVE
				if( any( lessThan( viewPosition.xy, boundingBox.min ) ) || any( greaterThan( viewPosition.xy, boundingBox.max ) ) ) {
					discard;
				}
		#endif

				VoxelInfo voxelInfo = processFragment();

				float deltaZ = 0.5 * fwidth( viewPosition.z );
				int minZ = int( viewPosition.z - deltaZ );
				int maxZ = int( ceil( viewPosition.z + deltaZ ) );

				for( int z = minZ; z <= maxZ; ++z ) {
					// this is identical to multiplying with the inverse
					// wtf @ +0.5
					ivec3 voxelPosition = ivec3( (vec3( viewPosition.xy, z ) + 0.5) * mainAxisPermutation[ mainAxis ] );

					splatVoxel( voxelPosition, voxelInfo );
				}

				//gl_FragColor = vec4( vec3(worldPosition) / 32.0, 1.0 );
			}

muxer:
	vertexShader

	code::
		// TODO: using a geometry shader to create full screen quads and the fragment shader for the actual operation might be better throughput wise?
		uniform restrict readonly layout(r32ui) uimage3D volumeChannels[4]; // nx nz ny hit
		uniform restrict writeonly layout(rgba8) image3D volume;

		uniform ivec3 sizeHelper; // size.x size.y size.x*size.y;

		void vertexShader() {
			ivec3 index3;
			index3.x = gl_InstanceID % sizeHelper.x;
			index3.y = (gl_InstanceID / sizeHelper.x) % sizeHelper.y;
			index3.z = (gl_InstanceID / sizeHelper.z);

			uvec4 packed_normalHit;
			for( int i = 0 ; i < 4 ; ++i ) {
				packed_normalHit[i] = imageLoad( volumeChannels[i], index3 )[0];
			}

			vec4 normalHit;
			normalHit.rgb = vec3( packed_normalHit.rgb ) / packed_normalHit[3] / 512.0; // 0..1.0
			normalHit.a = float( packed_normalHit[3] ) / 255.0;
			imageStore( volume, index3, normalHit );
		}
